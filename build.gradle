plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'idea'
  id 'com.gradle.plugin-publish' version '0.9.7'
  id 'com.palantir.git-version' version '0.2.0'
  id 'com.palantir.idea-test-fix' version '0.1.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = '1.7'

dependencies {
  compile localGroovy()
  compile gradleApi()
  compile 'org.codehaus.groovy:groovy-backports-compat23:2.3.5'
  testCompile gradleTestKit()
}

//// Tests ////////////////////////////////////////////////////////////////////////////

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

// Add the classpath file to the test runtime classpath
dependencies {
  testRuntime files(createClasspathManifest)
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

//// Publication //////////////////////////////////////////////////////////////////////

group = 'org.inferred'
version = (System.env.TRAVIS_TAG ?: gitVersion()).replaceAll('^v','')

pluginBundle {
  website = 'https://github.com/palantir/gradle-processors'
  vcsUrl = 'https://github.com/palantir/gradle-processors'

  plugins {
    greetingsPlugin {
      displayName = 'Processors plugin'
      id = 'org.inferred.processors'
      description = 'Clean integration of Java 6+ annotation processors with the Eclipse and IDEA plugins'
      tags = ['java', 'annotation', 'processor']
    }
  }
}

// Read publication secrets from environment
task setupPublishProperties << {
  def key = System.env.gradlePublishKey
  if (key) {
    System.properties.setProperty("gradle.publish.key", key)
  }
  def secret = System.env.gradlePublishSecret
  if (secret) {
    System.properties.setProperty("gradle.publish.secret", secret)
  }
}
tasks.publishPlugins.dependsOn tasks.setupPublishProperties
